mapscripts ChitinCragDale_Lab_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: ChitinCragDale_Lab_EventScript_OnTransition
}

script ChitinCragDale_Lab_EventScript_OnTransition {
    setvar(VAR_GRASS_SPECIES, SPECIES_BULBASAUR)
    setvar(VAR_FIRE_SPECIES, SPECIES_CHARMANDER)
    setvar(VAR_WATER_SPECIES, SPECIES_SQUIRTLE)
}

const PROF_BIRCH_ID = 1
const GRASS_BALL_ID = 2
const FIRE_BALL_ID = 3
const WATER_BALL_ID = 4

script ChitinCragDale_Lab_EventScript_ClickOnGrassBall {
    setvar(VAR_SELECTED_STARTER_TYPE, 0)
    call(ChitinCragDale_Lab_EventScript_PickSelectedStarter)
}
script ChitinCragDale_Lab_EventScript_ClickOnFireBall {
    setvar(VAR_SELECTED_STARTER_TYPE, 1)
    call(ChitinCragDale_Lab_EventScript_PickSelectedStarter)
}
script ChitinCragDale_Lab_EventScript_ClickOnWaterBall {
    setvar(VAR_SELECTED_STARTER_TYPE, 2)
    call(ChitinCragDale_Lab_EventScript_PickSelectedStarter)
}

script ChitinCragDale_Lab_EventScript_PickSelectedStarter {
    lock
    if(flag(FLAG_STARTER_GRASS) || flag(FLAG_STARTER_FIRE) || flag(FLAG_STARTER_WATER)) {
        msgbox(format("Don't be greedy!"))
        waitmessage
        release
        return
    }
    switch(var(VAR_SELECTED_STARTER_TYPE)) {
        case 0:
            copyvar(VAR_SELECTED_STARTER_SPECIES, VAR_GRASS_SPECIES)
        case 1:
            copyvar(VAR_SELECTED_STARTER_SPECIES, VAR_FIRE_SPECIES)
        case 2:
            copyvar(VAR_SELECTED_STARTER_SPECIES, VAR_WATER_SPECIES)
        default:
            msgbox(format("No starter selected."))
            waitmessage
            release
            return
    }

    showmonpic(VAR_SELECTED_STARTER_SPECIES, 10, 3)
    bufferspeciesname(STR_VAR_1, VAR_SELECTED_STARTER_SPECIES)
    msgbox(format("Wanna pick {STR_VAR_1}?"), MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES) {
        call(ChitinCragDale_Lab_EventScript_SetStartingFlags)
        givemon(VAR_SELECTED_STARTER_SPECIES, 50, ITEM_MASTER_BALL)
        bufferspeciesname(STR_VAR_1, VAR_SELECTED_STARTER_SPECIES)
        if(var(VAR_RESULT) == MON_CANT_GIVE){
            hidemonpic
            msgbox(format("No more room."))
        } else {
            playfanfare(MUS_OBTAIN_ITEM)
            msgbox(format("Received {STR_VAR_1}!"))
            switch(var(VAR_SELECTED_STARTER_TYPE)) {
                case 0:
                    removeobject(GRASS_BALL_ID)
                    setflag(FLAG_STARTER_GRASS)
                case 1:
                    removeobject(FIRE_BALL_ID)
                    setflag(FLAG_STARTER_FIRE)
                case 2:
                    removeobject(WATER_BALL_ID)
                    setflag(FLAG_STARTER_WATER)
            }


            if(var(VAR_RESULT) == MON_GIVEN_TO_PARTY){
                call(NicknamePartyMon)
            } 
            elif(var(VAR_RESULT) == MON_GIVEN_TO_PC) {
                call(NicknameBoxMon)
            }   
        }
    } else {
        hidemonpic  
        msgbox(format("{STR_VAR_1} sits there patiently."))
    }

    hidemonpic
    waitmessage
    release
    return
}


script ChitinCragDale_Lab_EventScript_SetStartingFlags {
    setflag(FLAG_SYS_POKEMON_GET)
    setflag(FLAG_SID_QOL_MENU_GET)
	setflag(FLAG_RESCUED_BIRCH)
	setflag(FLAG_HIDE_ROUTE_101_BIRCH_ZIGZAGOON_BATTLE)
	setflag(FLAG_ADVENTURE_STARTED)
	clearflag(FLAG_HIDE_LITTLEROOT_TOWN_BIRCHS_LAB_BIRCH)
	setflag(FLAG_HIDE_ROUTE_101_BIRCH_STARTERS_BAG)
	setvar(VAR_BIRCH_LAB_STATE, 2)
	setvar(VAR_ROUTE101_STATE, 3)
	setflag(FLAG_SYS_POKEDEX_GET)
	setflag(FLAG_SYS_POKENAV_GET)
	setflag(DN_FLAG_DEXNAV_GET)
	setflag(DN_FLAG_DETECTOR_MODE)
	special(SetUnlockedPokedexFlags)
	setflag(FLAG_RECEIVED_POKEDEX_FROM_BIRCH)
	setflag(OW_FLAG_POKE_RIDER)
	setvar(VAR_CABLE_CLUB_TUTORIAL_STATE, 2)
	setflag(FLAG_SYS_NATIONAL_DEX)
	special(EnableNationalPokedex)
	setflag(FLAG_RECEIVED_RUNNING_SHOES)
	setflag(FLAG_SYS_B_DASH)
    giveitem(ITEM_ACRO_BIKE)
    giveitem(ITEM_MASTER_BALL, 50)
    giveitem(ITEM_TM01)
    giveitem(ITEM_TM02)
    giveitem(ITEM_TM03)
    giveitem(ITEM_TM04)
    giveitem(ITEM_TM05)
    giveitem(ITEM_TM24)
    // 4: Received Running Shoes
	setvar(VAR_LITTLEROOT_TOWN_STATE, 4) 
    // 7: Told to go meet rival
	setvar(VAR_LITTLEROOT_INTRO_STATE, 7) 
    // 2: Met Rival's Mom (and is corresponding gender)
	setvar(VAR_LITTLEROOT_HOUSES_STATE_BRENDAN, 2) 
    // 2: Met Rival's Mom (and is corresponding gender)
	setvar(VAR_LITTLEROOT_HOUSES_STATE_MAY, 2) 
    // 4: Received Pokedex
	setvar(VAR_LITTLEROOT_RIVAL_STATE, 4) 
}

script ChitinCragDale_Lab_EventScript_EditGeneration {
    lock
    faceplayer
    
    if(flag(FLAG_STARTER_GRASS) || flag(FLAG_STARTER_FIRE) || flag(FLAG_STARTER_WATER)) {
        msgbox(format("Enjoy your starter!"))
        waitmessage
        release
        end
    }
    
    msgbox(format("Let's configure your playthrough!"))
    waitmessage
    call(ChitinCragDale_Lab_EventScript_EditGrassStarter)
    waitmessage
    call(ChitinCragDale_Lab_EventScript_EditFireStarter)
    waitmessage
    call(ChitinCragDale_Lab_EventScript_EditWaterStarter)
    waitmessage
    msgbox(format("Playthrough configured!"))
    waitmessage
    release
    end
}

script ChitinCragDale_Lab_EventScript_EditGrassStarter {
    msgbox(format("Which region founded the {COLOR GREEN}{SHADOW LIGHT_GREEN}Vigor{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} province?"))

    dynmultipush("1: Kanto", SPECIES_BULBASAUR)
    dynmultipush("2: Johto", SPECIES_CHIKORITA)
    dynmultipush("3: Hoenn", SPECIES_TREECKO)
    dynmultipush("4: Sinnoh", SPECIES_TURTWIG)
    dynmultipush("5: Unova", SPECIES_SNIVY)
    dynmultipush("6: Kalos", SPECIES_CHESPIN)
    dynmultipush("7: Alola", SPECIES_ROWLET)
    dynmultipush("8: Galar", SPECIES_GROOKEY)
    dynmultipush("9: Paldea", SPECIES_SPRIGATITO)
    
    dynmultistack(0, 0, TRUE, 6, FALSE, SPECIES_BULBASAUR, DYN_MULTICHOICE_CB_SHOW_MON)
    copyvar(VAR_GRASS_SPECIES, VAR_RESULT)
    bufferspeciesname(STR_VAR_3, VAR_GRASS_SPECIES)
    msgbox(format("{COLOR GREEN}{SHADOW LIGHT_GREEN}{STR_VAR_3}{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} is now available as the grass starter!"))		
    return
}

script ChitinCragDale_Lab_EventScript_EditFireStarter {
    msgbox(format("Which region founded the {COLOR RED}{SHADOW LIGHT_RED}Ardor{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} province?"))

    dynmultipush("1: Kanto", SPECIES_CHARMANDER)
    dynmultipush("2: Johto", SPECIES_CYNDAQUIL)
    dynmultipush("3: Hoenn", SPECIES_TORCHIC)
    dynmultipush("4: Sinnoh", SPECIES_CHIMCHAR)
    dynmultipush("5: Unova", SPECIES_TEPIG)
    dynmultipush("6: Kalos", SPECIES_FENNEKIN)
    dynmultipush("7: Alola", SPECIES_LITTEN)
    dynmultipush("8: Galar", SPECIES_SCORBUNNY)
    dynmultipush("9: Paldea", SPECIES_FUECOCO)

    dynmultistack(0, 0, TRUE, 6, FALSE, SPECIES_CHARMANDER, DYN_MULTICHOICE_CB_SHOW_MON)
    copyvar(VAR_FIRE_SPECIES, VAR_RESULT)
    bufferspeciesname(STR_VAR_3, VAR_FIRE_SPECIES)
    msgbox(format("{COLOR RED}{SHADOW LIGHT_RED}{STR_VAR_3}{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} is now available as the fire starter!"))		
    return
}



script ChitinCragDale_Lab_EventScript_EditWaterStarter {
    msgbox(format("Which region founded the {COLOR BLUE}{SHADOW LIGHT_BLUE}Guile{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} province?"))
    
    dynmultipush("1: Kanto", SPECIES_SQUIRTLE)
    dynmultipush("2: Johto", SPECIES_TOTODILE)
    dynmultipush("3: Hoenn", SPECIES_MUDKIP)
    dynmultipush("4: Sinnoh", SPECIES_PIPLUP)
    dynmultipush("5: Unova", SPECIES_OSHAWOTT)
    dynmultipush("6: Kalos", SPECIES_FROAKIE)
    dynmultipush("7: Alola", SPECIES_POPPLIO)
    dynmultipush("8: Galar", SPECIES_SOBBLE)
    dynmultipush("9: Paldea", SPECIES_QUAXLY)
    
    dynmultistack(0, 0, TRUE, 6, FALSE, SPECIES_SQUIRTLE, DYN_MULTICHOICE_CB_SHOW_MON)
    copyvar(VAR_WATER_SPECIES, VAR_RESULT)
    bufferspeciesname(STR_VAR_3, VAR_WATER_SPECIES)
    msgbox(format("{COLOR BLUE}{SHADOW LIGHT_BLUE}{STR_VAR_3}{COLOR DARK_GRAY}{SHADOW LIGHT_GRAY} is now available as the water starter!"))	
    return
}

script NicknamePartyMon {
    msgbox(format("Give it a nickname?"), MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES) {
        call(Common_EventScript_GetGiftMonPartySlot)
        call(Common_EventScript_NameReceivedPartyMon)
    }
    return
}

script NicknameBoxMon {
    msgbox(format("Give it a nickname?"), MSGBOX_YESNO)
    hidemonpic
    if(var(VAR_RESULT) == YES) {
        call(Common_EventScript_NameReceivedBoxMon)
    } 
    call(Common_EventScript_TransferredToPC)
    return
}